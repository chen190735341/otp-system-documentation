* auto-gen TOC:
{:toc}

# 2 系统原理
## 2.1 系统原理
### 2.1.1 系统启动

Erlang的运行环境(runtime system)通过命令`erl`启动：

    % erl
    Erlang/OTP 17 [erts-6.0] [hipe] [smp:8:8]
    Eshell V6.0 (abort with ^G)
    1>
    
erl有许多启动参数，具体可以参见`erl(1)`。本章将对部分参数进行介绍。

erlang应用程序可以通过函数`init:get_argument(Key)`，`init:get_arguments()`获取命令行参数。参见`init(3)`。

### 2.1.2 重启和退出

Erlang系统可以通过调用`halt/0,1`退出。参见`erlang(3)`。

`init`模块包含重新启动(restarting）、重新引导（rebooting）和停止（stopping）Erlang运行环境的函数。参见`init(3)`。

    init:restart()
    init:reboot()
    init:stop()
    
另外Erlang外壳（shell）如果停止，Erlang运行时也将停止。

### 2.1.3 启动脚本

Erlang运行环境依赖启动脚本（`boot script`）进行启动。启动脚本包含了需要加载的代码、进程和应用的指令。

启动脚本的扩展名是`.script`。Erlang系统启动是用的是二进制版本的启动脚本，它的扩展名为`.boot`。

用命令行参数`-boot`指定用哪个启动脚本启动Erlang。启动脚本的扩展名`.boot`必须省略。例如，用start_all.boot启动：

    % erl -boot start_all
    
如果没有指定启动脚本，erl将用默认的启动脚本，即：`ROOT/bin/start`进行启动。下面展示了默认启动脚本的内容。

通过命令行参数`-init_debug`可以让`init`进行在处理启动脚本时打印相应调试信息。

    % erl -init_debug
    {progress,preloaded}
    {progress,kernel_load_completed}
    {progress,modules_loaded}
    {start,heart}
    {start,error_logger}
    ...
    
启动脚本的具体内容和语法描述参见`script(4)`
  
**默认启动脚本**
  
Erlang/OTP自带了两个启动脚本：
  
- `start_clean.boot`
:    加载并启动Kernel和STDLIB
- `start_sasl.boot`
:    加载并启动Kernel、STDLIB和SASL
- `no_dot_erlang.boot`
:    加载并启动Kernel、STDLIB，但是不加载`.erlang`文件。这对于那些忽略用户自定义环境的脚本和工具非常有用。

在用`Install`安装Erlang/OTP时，程序会询问：“你希望精简启动，还是SASL启动？”，用以指定默认使用`start_clean`还是`start_sasl`启动脚本。如果你回答前者，则Erlang将用`start_clean`反之则用`start_sasl`。安装程序将复制你指定的启动脚本到`ROOT/bin`目录下，并命名为`start.boot`作为默认启动脚本。

**用户自定义启动脚本**

有时候创建自定义的启动脚本非常有用并且有必要。尤其是在嵌入式模式下运行Erlang，参见`代码加载策略(Code Loading Strategy)`。

当然你可以手动写一个启动脚本。但是最好还是用函数`systools:make_script/1,2`从发布信息文件`Name.rel`中自动生成。这样做的前提是你的程序源代码结构符合OTP的设计规范。(但是程序并不要求以OTP方式启动，可以是普通的Erlang应用)

更多关于`.erl`文件的内容参见OTP设计原理和`erl(4)`。

二进制的启动脚本`Name.boot`可以通过`systools:script2boot(File)`函数从`Name.script`中产生。

### 2.1.4 代码加载策略

Erlang可以启动为嵌入(`embedded`)或交互(`interactive`)两种模式。模式的选择通过命令行参数`-mode`指定。

    %erl -mode embedded
    
默认情况为交互模式。

- 在嵌入模式下，系统在启动时将根据启动脚本加载所有的代码。（代码可以通过显示地命令代码服务器进行延迟加载）
- 在交互模式下，代码将在第一次引用时进行加载。某个模块的函数被调用，但模块尚未加载时，代码服务器将搜索代码路径并将其加载到系统中。

初始情况下，代码路径包括当前工作路径和所有`ROOT/lib`下的代码路径，其中`ROOT`为Erlang/OTP的安装路径。路径以`Name[-Vsn]`形式命名，其中后缀`-Vsn`表示版本，并且是可选的。代码服务器将选择同一名称下版本最高的加载。代码服务器将加载`Name[-Vsn]`目录下的`ebin`文件夹。

在erl的启动参数中可以指定额外的代码路径，形式为：`-pa Directories`和`-pz Directories`。前者将路径加载到代码路径列表的开头，后者追加在最后。例如

    %erl -pa /home/arne/mycode
    
代码服务器模块`code`包含许多可以更改和检查搜索路径的函数，具体参见`code(3)`。

### 2.1.5 文件类型

Erlang/OTP中定义了如下文件类型：

| 文件类型 | 名称 / 扩展名 | 参考资料 
-----------|-------------------|-----------|
模块代码 | .erl | Erlang Reference Manual
头文件 | .hrl | Erlang Reference Manual
发布资源文件 | .rel | rel(4)
应用资源文件 | .app | app(4)
启动脚本 | .script | script(4)
二进制启动脚本 | .boot | -
配置文件 | .config | config(4)
应用升级文件 | .appup | appup(4) 
发布升级文件 | relup | relup(4)

表1.1 文件类型

## 2.2 错误日志
### 2.2.1 运行系统产生的错误信息

系统产生的错误信息记录了系统中进程未捕获的错误异常，默认情况下打印到终端(tty):

    =ERROR REPORT==== 9-Dec-2003::13:25:02 ===
    Error in process <0.27.0> with exit value: {{badmatch,[1,2,3]},[{m,f,1},{shell,eval_loop,2}]}

错误信息由`error logger`处理，它是注册名为`error_logger`的系统进程。该进程接收所有来自Erlang运行系统的错误信息，包括标准行为或Erlang/OTP的应用。

退出原因的描述(例如`badarg`参数错误)参见Erlang参考手册(Erlang Reference Manual)中的`Errors and Error Handling`。

`error_logger`进程的用户接口参见`error_logger(3)`。可以配置系统让其将错误信息写入文件中。而且用户可以通过`error_logger`定义应用产生的错误信息和其格式。

### 2.2.2 SASL 错误日志

标准进程(如`supervisor`,`gen_server`等)发送错误信息到error_logger。如果启动了SASL，这些信息也将被写入到终端。进一步阅读请移步SASL用户指南中的`SASL Error Logging`。

    % erl -boot start_sasl
    Erlang (BEAM) emulator version 5.4.13 [hipe] [threads:0] [kernel-poll]
    =PROGRESS REPORT==== 31-Mar-2006::12:45:58 ===
            supervisor: {local,sasl_safe_sup}
                started: [{pid,<0.33.0>},
                        {name,alarm_handler},
                        {mfa,{alarm_handler,start_link,[]}},
                        {restart_type,permanent},
                        {shutdown,2000},
                        {child_type,worker}]
    =PROGRESS REPORT==== 31-Mar-2006::12:45:58 ===
            supervisor: {local,sasl_safe_sup}
                started: [{pid,<0.34.0>},
                        {name,overload},
                        {mfa,{overload,start_link,[]}},
                        {restart_type,permanent},
                        {shutdown,2000},
                        {child_type,worker}]
    =PROGRESS REPORT==== 31-Mar-2006::12:45:58 ===
            supervisor: {local,sasl_sup}
                started: [{pid,<0.32.0>},
                        {name,sasl_safe_sup},
                        {mfa,{supervisor,
                        start_link,
                        [{local,sasl_safe_sup},sasl,safe]}},
                        {restart_type,permanent},
                        {shutdown,infinity},
                        {child_type,supervisor}]
    =PROGRESS REPORT==== 31-Mar-2006::12:45:58 ===
            supervisor: {local,sasl_sup}
                started: [{pid,<0.35.0>},
                    {name,release_handler},
                    {mfa,{release_handler,start_link,[]}},
                    {restart_type,permanent},
                    {shutdown,2000},
                    {child_type,worker}]
    =PROGRESS REPORT==== 31-Mar-2006::12:45:58 ===
            application: sasl
                started_at: nonode@nohost
    Eshell V5.4.13 (abort with ^G)
    1>
  
## 2.3 创建和升级目标系统

### 2.3.1 概述

创建Erlang/OTP应用系统最简单的方法是在特定的地方安装Erlang/OTP，并且在其他特定的地方安装程序的代码，然后启动Erlang运行系统是将应用的代码路径加入到搜索路径当中。

但是这样做常常不尽人意。一个开发者可能会为某个特定的目的开发一个Erlang/OTP应用，同时Erlang/OTP一些自带的应用在这种情况下可能变得不相关。因此，有必要根据现有的Erlang/OTP系统创建一个符合目标需求的，包含了新的应用，同时去除不相关应用的Erlang系统。那些不相关的源代码和文档也将排除在外。

本章将讨论如何创建这样的系统，我们称之为目标系统(`target system`)。

在一下章节中我们将创建具有如下不同需求能力的目标系统:

- 基本目标系统，它可以通过`erl`脚本进行启动
- 精简目标系统，系统仍然可以在运行时进行代码替换
- 嵌入式目标系统，系统仍然支持将日志输出到文件以备将来查看，同时系统将随着操作启动引导时自动启动

我们只讨论Erlang/OTP在UNIX环境下运行。

在`sasl`应用中有一个范例模块`target_system.erl`包含了创建和安装目标系统的函数。下文将使用该模块进行阐述，在本章的末尾将列出该模块的源代码。

### 2.3.2 创建目标系统

本文假设你的工作符合OTP设计规范。

**第1步**: 创建`.erl`文件(参见`rel(4)`)，文件指定`erts`的版本和需要包含的所用Erlang应用。下面是一个文件示例`mysystem.rel`：

    %% mysystem.rel
    {release,
        {"MYSYSTEM", "FIRST"},
        {erts, "5.10.4"},
        [{kernel, "2.16.4"},
            {stdlib, "1.19.4"},
            {sasl, "2.3.4"},
            {pea, "1.0"}]}.

列出的应用包含自带的Erlang/OTP应用和你自己写的新应用，如例子中的`pea`。

**第2步**: 在`mysystem.rel`所在的目录下启动Erlang/OTP系统。

    os>erl -pa /home/user/target_system/myapps/pea-1.0/ebin
    
启动时指定`pea-1.0`的ebin目录。

**第3步**: 现在创建目标系统：

    1>target_system:create("mysystem").
    
`target_system:create/1`做了如下事情：
 - 读取`mysystem.rel`文件，创建一个一模一样的`plain.rel`文件，唯一的区别是，文件之列出了`kernel`和`stdlib`应用。
 - 从`mysystem.rel`和`plain.rel`文件通过函数`systools:make_script/2`创建`mysystem.script`,`mysystem.boot`,`plain.rel`,`plain.boot`。
 - 用`systools:make_tar/2`创建打包文件`mysystem.tar.gz`。打包文件包含如下内容：


    erts-5.10.4/bin/
    releases/FIRST/start.boot
    releases/FIRST/mysystem.rel
    releases/mysystem.rel
    lib/kernel-2.16.4/
    lib/stdlib-1.19.4/
    lib/sasl-2.3.4/
    lib/pea-1.0/
    
`release/FIRST/start.boot`文件是`mysystem.boot`的副本。

打包文件中有两个`mysystem.rel`文件。刚开始，该文件值存在于`release`目录下，以便让`release_handler`可以提取它。解压之后，`release_handle`会自动将它复制到`release/FIRST`。但有时候打包文件在不包含`release_handler`的情况下解压(如，在解压第一个目标系统时)此时则有必要手动进行复制。

- 创建`tmp`临时目录，并将打包文件解压到其中。
- 删除`tmp/erts-5.10.4/bin`目录下的`erl`和`start`文件。这两个文件将在安装时自动创建。
- 创建目录`tmp/bin`
- 将先前创建的`plain.boot`文件复制到`tmp/bin/start.boot`中。
- 将`tmp/erts-5.10.4/bin`目录中的`empd`,`run_erl`和`to_erl`文件复制到`tmp/bin`目录中
- 创建`tmp/log`目录，如果系统用`bin/start`以嵌入模式启动将用到它。
- 创建`tmp/releases/start_erl.data`文件，该文件内容为“5.10.4 FIRST”。该文件将作为数据文件传递给`start_erl`脚本
- 将`tmp`下的文件重新打包成`mysystem.tar.gz`，同时删除`tmp`目录。

### 2.3.3 安装目标系统

**第4步**: 在合适的目录下安装目标系统。

    2> target_system:install("mysystem", "/usr/local/erl-target").
    
`target_system:install/2`做了如下事情：
- 将`mysystem.tar.gz`解压到目标目录`/usr/local/erl-target`中
- 读取目标目录中`release/start_erl.data`文件，以确定Erlang运行系统的版本("5.10.4")
- 将`erl.src`,`start.src`以及`start_erl.src`文件中的`%FINAL_BOOTDIR`和`%EMU`变量（分别表示`/usr/local/erl-target`和`beam`）替换为目标目录`erts-5.10.4/bin`。然后将结果文件放入目标`/bin`目录中。
- 最后从`releases/myystem.erl`创建目标文件`releases/RELEASES`。


### 2.3.4 启动目标系统

现在目标系统可以以多种方式启动。

同过如下命令启动基本目标系统

    os>/usr/local/erl-target/bin/erl

该命令只启动了`kernel`和`stdlib`，可用作如，一般的开发。这些是需要两个文件分别为`bin/erl`(来源于`erts-5.10.4、bin/erl.src`)和`bin/start.boot`文件(`plain.boot`文件的副本)。

我们也可以启动分布式系统(需要`bin/epmd`)。

如果需要启动`mysystem.rel`文件中的所有应用，用`-boot`参数按如下方式启动：

    os>/usr/local/erl-target/bin/erl -boot /usr/local/erl-target/releases/FIRST/start
    
我们用以上方式启动精简目标系统。唯一的区别在于，`releases/RELEASES`文件将用于运行时代码替换。

用`bin/start`可以启动嵌入式目标系统。该脚本调用`bin/run_erl`，它又调用了`bin/start_erl`（大体上，`start_erl`是`erl`的嵌入式变体）。

`start`脚本是在安装时从`erts-5.10.4/bin/start.src`产生的，仅仅是一个例子。你应该根据需要修改它。典型地，可以在UNIX系统引导的时候运行它。

`run_erl`简单的封装，让系统将日志输出到文件中。同时它提供了一个简单的可以连接到Erlang外壳的机制(`to_erl`)。

`start_erl`需要根目录("`user/local/erl-target`")和，发布目录("`/usr/local/erl-target/releases`")，以及`start_erl.data`文件。它从`start_erl.data`文件中读取Erlang运行系统的版本信息("5.10.4")，和发布版本("FIRST")，并用"releases/FIRST/start.boot"中指定的`-boot`参数启动运行系统。

`start_erl`同时假定发布目录("releases/FIRST/sys.config")存在`sys.config`文件。该部分内容见将在下节进行讨论。

用户最后不要修改`start_erl`脚本。

    
### 2.3.5 系统配置参数
正如上述提到的一样`start_erl`需要`sys.config`文件并且在发布目录下("releases/FIRST/sys.config")。如果没有该目录，系统将无法启动，所以必须提供该文件。

如果你的系统配置数据不依赖与位置和场所，那么可以方便地在开始的时候用`target_system:create/1`创建，这样它就可以成为打包文件的一部分。事实上如果你在创建`mysystem.rel`文件的地方创建`sys.config`，`sys.config`文件也会被放置在适当的地方。

### 2.3.6 安装函数与Install shell脚本的区别

上文提到的`install/2`过程有别与`Install`shell脚本。事实上，`create/1`将尽可能创建完整的发布包裹，然后让`install/2`处理一些与位置相关的文件。

### 2.3.7 创建下一个版本
该例子中对应用`pea`进行了更改，同时更改包括`erts`,`kernel`,`stdlib`和`sasl`。

**第1步**: 创建`.rel`文件：

    %% mysystem2.rel
	{release,
		{"MYSYSTEM", "SECOND"},
		{erts, "6.0"}, 
		[{kernel, "3.0"},
			{stdlib, "2.0"},
			{sasl, "2.4"},
			{pea, "2.0"}]}.

**第2步**: 为`pea`应用创建升级文件，例如：

	%% pea.appup
	{"2.0",
		[{"1.0",[{load_module,pea_lib}]}],
		[{"1.0",[{load_module,pea_lib}]}]}.

**第3步**: 在`mysystem2.rel`所在目录启动Eralng/OTP：

	os> erl -pa /home/user/target_system/myapps/pea-2.0/ebin

启动时指定新版本的`pea`的路径。

**第4步**: 创建发布升级文件(参见`relup(4)`):

	1> systools:make_relup("mysystem2", ["mysystem"], ["mysystem"], [{path, ["/home/user/target_system/myapps/pea-1.0/ebin"]}]).

其中"mysystem"是基础版本的，"mysystem2"是升级版本。

值得注意的是`path`选项为旧版本的路径。(新的版本已经在代码搜索路径中——假定当前运行在正确的Erlang/OTP版本)

**第5步**: 创建新的发布

	2> target_system:create("mysystem2").

上一步中创建的`relup`文件将被自动加入发布包中。

### 2.3.8 升级目标系统
该部分内容需要在目标节点完成，并且假定节点以嵌入模式运行并且开启了`-heart`选项，该选项将使节点自动重启。更多信息参见上文中的启动目标系统`*Starting a Target System*`。

我们为`bin/start`添加了`-heart`选项。

	#!/bin/sh
    ROOTDIR=/usr/local/erl-target/
	if [ -z "$RELDIR" ]
	then
		RELDIR=$ROOTDIR/releases
	fi
	START_ERL_DATA=${1:-$RELDIR/start_erl.data}
	$ROOTDIR/bin/run_erl -daemon /tmp/ $ROOTDIR/log "exec $ROOTDIR/bin/start_erl $ROOTDIR $RELDIR $START_ERL_DATA

同时我们用最简单的`sys.config`，它保存在`releases/FIRST`:

	%% sys.config
	[].

最后，为了准备升级，我们必须将新的发布包放置在目标系统的`releases`文件夹下。

	os> cp mysystem2.tar.gz /usr/local/erl-target/releases

并且假定节点按如下方式启动：

	os> /usr/local/erl -target/bin/start

它可以通过如下方式访问：

	os> /usr/local/erl-target/bin/to_erl /tmp/erlang.pipe.1

节点的日志保存在`/usr/local/erl-target/log`。该路劲是`run_erl`启动脚本中的一个参数。

**第1步** 解压发布包:
	
	1> {ok, Vsn} = release_handler:unpack_release("mysystem2").

**第2步** 安装发布包：

	2> release_handler:install_release(Vsn).
	{continue_after_restart,"FIRST",[]}
	heart: Tue Apr 1 12:15:10 2014: Erlang has closed.
	heart: Tue Apr 1 12:15:11 2014: Executed "/usr/local/erl-target/bin/start /usr/local/erl-target/releases/new_start_[End]	

上文中调用`release_handler:install_release/1`的输出表示`release_handler`已经将节点用`heart`模式进行了重启。当升级内容包括`erts`,`kernel`,`stdlib`或者`sasl`是节点都将被重启。更多内容参见Erlang/OTP变更的升级`Upgrade when Erlang/OTP has Changed`。

此时节点可以通过新的管道进行连接。

	os> /usr/local/erl-target/bin/to_erl /tmp/erlang.pipe.2

现在让我们看看系统有哪些版本。

	1> release_handler:which_releases().
	[{"MYSYSTEM","SECOND",
			["kernel-3.0","stdlib-2.0","sasl-2.4","pea-2.0"],
			current},
		{"MYSYSTEM","FIRST",
			["kernel-2.16.4","stdlib-1.19.4","sasl-2.3.4","pea-1.0"],
			permanent}]

可以看到现在运行的版本是"SECOND"，然而"FIRST"仍然是永久版本。这意味着如果节点现在重启的话，节点将重新运行"FIRST"版本。

**第3步** 让新版本成为永久版本:

	> release_handler:make_permanent("SECOND").

现在重新看下版本信息：

	2> release_handler:which_releases().
	[{"MYSYSTEM","SECOND",
		["kernel-3.0","stdlib-2.0","sasl-2.4","pea-2.0"],
		permanent},
	{"MYSYSTEM","FIRST",
		["kernel-2.16.4","stdlib-1.19.4","sasl-2.3.4","pea-1.0"],old}]

可以看到现在新版本已经变成永久性的了，重启节点也没关系了。

### 2.3.9 `target_system.erl 源代码
该模块在`sasl`应用下`examples`目录中可以找到。

```erlang
-module(target_system).
-export([create/1, create/2, install/2]).
%% Note: RelFileName below is the *stem* without trailing .rel,
%% .script etc.
%%
%% create(RelFileName)
%%
create(RelFileName) ->
	create(RelFileName,[]).
create(RelFileName,SystoolsOpts) ->
	RelFile = RelFileName ++ ".rel",
	Dir = filename:dirname(RelFileName),
	PlainRelFileName = filename:join(Dir,"plain"),
	PlainRelFile = PlainRelFileName ++ ".rel",
	io:fwrite("Reading file: ~tp ...~n", [RelFile]),
	{ok, [RelSpec]} = file:consult(RelFile),
	io:fwrite("Creating file: ~tp from ~tp ...~n",
			  [PlainRelFile, RelFile]),
	{release,
	 {RelName, RelVsn},
	 {erts, ErtsVsn},
	 AppVsns} = RelSpec,
	PlainRelSpec = {release,
					{RelName, RelVsn},
					{erts, ErtsVsn},
					lists:filter(fun({kernel, _}) ->
										 true;
									({stdlib, _}) ->
										 true;
									(_) ->
										 false
								 end, AppVsns)
				   },
	{ok, Fd} = file:open(PlainRelFile, [write]),
	io:fwrite(Fd, "~p.~n", [PlainRelSpec]),
	file:close(Fd),
	io:fwrite("Making \"~ts.script\" and \"~ts.boot\" files ...~n",
			  [PlainRelFileName,PlainRelFileName]),
	make_script(PlainRelFileName,SystoolsOpts),
	io:fwrite("Making \"~ts.script\" and \"~ts.boot\" files ...~n",
			  [RelFileName, RelFileName]),
	make_script(RelFileName,SystoolsOpts),
	TarFileName = RelFileName ++ ".tar.gz",
	io:fwrite("Creating tar file ~tp ...~n", [TarFileName]),
	make_tar(RelFileName,SystoolsOpts),
	TmpDir = filename:join(Dir,"tmp"),
	io:fwrite("Creating directory ~tp ...~n",[TmpDir]),
	file:make_dir(TmpDir),
	io:fwrite("Extracting ~tp into directory ~tp ...~n", [TarFileName,TmpDir]),
	extract_tar(TarFileName, TmpDir),
	TmpBinDir = filename:join([TmpDir, "bin"]),
	ErtsBinDir = filename:join([TmpDir, "erts-" ++ ErtsVsn, "bin"]),
	io:fwrite("Deleting \"erl\" and \"start\" in directory ~tp ...~n",
			  [ErtsBinDir]),
	file:delete(filename:join([ErtsBinDir, "erl"])),
	file:delete(filename:join([ErtsBinDir, "start"])),
	io:fwrite("Creating temporary directory ~tp ...~n", [TmpBinDir]),
	file:make_dir(TmpBinDir),
	io:fwrite("Copying file \"~ts.boot\" to ~tp ...~n",
			  [PlainRelFileName, filename:join([TmpBinDir, "start.boot"])]),
	copy_file(PlainRelFileName++".boot",filename:join([TmpBinDir, "start.boot"])),
	io:fwrite("Copying files \"epmd\", \"run_erl\" and \"to_erl\" from \n"
"~tp to ~tp ...~n",
			  [ErtsBinDir, TmpBinDir]),
	copy_file(filename:join([ErtsBinDir, "epmd"]),
			  filename:join([TmpBinDir, "epmd"]), [preserve]),
	copy_file(filename:join([ErtsBinDir, "run_erl"]),
			  filename:join([TmpBinDir, "run_erl"]), [preserve]),
	copy_file(filename:join([ErtsBinDir, "to_erl"]),
			  filename:join([TmpBinDir, "to_erl"]), [preserve]),
	%% This is needed if 'start' script created from 'start.src' shall
	%% be used as it points out this directory as log dir for 'run_erl'
	TmpLogDir = filename:join([TmpDir, "log"]),
	io:fwrite("Creating temporary directory ~tp ...~n", [TmpLogDir]),
	ok = file:make_dir(TmpLogDir),
	StartErlDataFile = filename:join([TmpDir, "releases", "start_erl.data"]),
	io:fwrite("Creating ~tp ...~n", [StartErlDataFile]),
	StartErlData = io_lib:fwrite("~s ~s~n", [ErtsVsn, RelVsn]),
	write_file(StartErlDataFile, StartErlData),
	io:fwrite("Recreating tar file ~tp from contents in directory ~tp ...~n",
			  [TarFileName,TmpDir]),
	{ok, Tar} = erl_tar:open(TarFileName, [write, compressed]),
	%% {ok, Cwd} = file:get_cwd(),
	%% file:set_cwd("tmp"),
	ErtsDir = "erts-"++ErtsVsn,
	erl_tar:add(Tar, filename:join(TmpDir,"bin"), "bin", []),
	erl_tar:add(Tar, filename:join(TmpDir,ErtsDir), ErtsDir, []),
	erl_tar:add(Tar, filename:join(TmpDir,"releases"), "releases", []),
	erl_tar:add(Tar, filename:join(TmpDir,"lib"), "lib", []),
	erl_tar:add(Tar, filename:join(TmpDir,"log"), "log", []),
	erl_tar:close(Tar),
	%% file:set_cwd(Cwd),
	io:fwrite("Removing directory ~tp ...~n",[TmpDir]),
	remove_dir_tree(TmpDir),
	ok.
install(RelFileName, RootDir) ->
	TarFile = RelFileName ++ ".tar.gz",
	io:fwrite("Extracting ~tp ...~n", [TarFile]),
	extract_tar(TarFile, RootDir),
	StartErlDataFile = filename:join([RootDir, "releases", "start_erl.data"]),
	{ok, StartErlData} = read_txt_file(StartErlDataFile),
	[ErlVsn, _RelVsn| _] = string:tokens(StartErlData, " \n"),
	ErtsBinDir = filename:join([RootDir, "erts-" ++ ErlVsn, "bin"]),
	BinDir = filename:join([RootDir, "bin"]),
	io:fwrite("Substituting in erl.src, start.src and start_erl.src to "
"form erl, start and start_erl ...\n"),
			  subst_src_scripts(["erl", "start", "start_erl"], ErtsBinDir, BinDir,
								[{"FINAL_ROOTDIR", RootDir}, {"EMU", "beam"}],
								[preserve]),
			  %%! Workaround for pre OTP 17.0: start.src and start_erl.src did
			  %%! not have correct permissions, so the above 'preserve' option did not help
			  ok = file:change_mode(filename:join(BinDir,"start"),8#0755),
			  ok = file:change_mode(filename:join(BinDir,"start_erl"),8#0755),
			  io:fwrite("Creating the RELEASES file ...\n"),
			  create_RELEASES(RootDir, filename:join([RootDir, "releases",
													  filename:basename(RelFileName)])).
%% LOCALS
%% make_script(RelFileName,Opts)
%%
make_script(RelFileName,Opts) ->
	systools:make_script(RelFileName, [no_module_tests,
									   {outdir,filename:dirname(RelFileName)}
									   |Opts]).
%% make_tar(RelFileName,Opts)
%%
make_tar(RelFileName,Opts) ->
	RootDir = code:root_dir(),
	systools:make_tar(RelFileName, [{erts, RootDir},
									{outdir,filename:dirname(RelFileName)}
									|Opts]).
%% extract_tar(TarFile, DestDir)
%%
extract_tar(TarFile, DestDir) ->
	erl_tar:extract(TarFile, [{cwd, DestDir}, compressed]).
create_RELEASES(DestDir, RelFileName) ->
	release_handler:create_RELEASES(DestDir, RelFileName ++ ".rel").
subst_src_scripts(Scripts, SrcDir, DestDir, Vars, Opts) ->
	lists:foreach(fun(Script) ->
						  subst_src_script(Script, SrcDir, DestDir,
										   Vars, Opts)
				  end, Scripts).
subst_src_script(Script, SrcDir, DestDir, Vars, Opts) ->
	subst_file(filename:join([SrcDir, Script ++ ".src"]),
			   filename:join([DestDir, Script]),
			   Vars, Opts).
subst_file(Src, Dest, Vars, Opts) ->
	{ok, Conts} = read_txt_file(Src),
	NConts = subst(Conts, Vars),
	write_file(Dest, NConts),
	case lists:member(preserve, Opts) of
		true ->
			{ok, FileInfo} = file:read_file_info(Src),
			file:write_file_info(Dest, FileInfo);
		false ->
			ok
	end.
%% subst(Str, Vars)
%% Vars = [{Var, Val}]
%% Var = Val = string()
%% Substitute all occurrences of %Var% for Val in Str, using the list
%% of variables in Vars.
%%
subst(Str, Vars) ->
	subst(Str, Vars, []).
subst([$%, C| Rest], Vars, Result) when $A =< C, C =< $Z ->
	subst_var([C| Rest], Vars, Result, []);
subst([$%, C| Rest], Vars, Result) when $a =< C, C =< $z ->
	subst_var([C| Rest], Vars, Result, []);
subst([$%, C| Rest], Vars, Result) when C == $_ ->
	subst_var([C| Rest], Vars, Result, []);
subst([C| Rest], Vars, Result) ->
	subst(Rest, Vars, [C| Result]);
subst([], _Vars, Result) ->
	lists:reverse(Result).
subst_var([$%| Rest], Vars, Result, VarAcc) ->
	Key = lists:reverse(VarAcc),
	case lists:keysearch(Key, 1, Vars) of
		{value, {Key, Value}} ->
			subst(Rest, Vars, lists:reverse(Value, Result));
		false ->
			subst(Rest, Vars, [$%| VarAcc ++ [$%| Result]])
	end;
subst_var([C| Rest], Vars, Result, VarAcc) ->
	subst_var(Rest, Vars, Result, [C| VarAcc]);
subst_var([], Vars, Result, VarAcc) ->
	subst([], Vars, [VarAcc ++ [$%| Result]]).
copy_file(Src, Dest) ->
	copy_file(Src, Dest, []).
copy_file(Src, Dest, Opts) ->
	{ok,_} = file:copy(Src, Dest),
	case lists:member(preserve, Opts) of
		true ->
			{ok, FileInfo} = file:read_file_info(Src),
			file:write_file_info(Dest, FileInfo);
		false ->
			ok
	end.
write_file(FName, Conts) ->
	Enc = file:native_name_encoding(),
	{ok, Fd} = file:open(FName, [write]),
	file:write(Fd, unicode:characters_to_binary(Conts,Enc,Enc)),
	file:close(Fd).
read_txt_file(File) ->
	{ok, Bin} = file:read_file(File),
	{ok, binary_to_list(Bin)}.
remove_dir_tree(Dir) ->
	remove_all_files(".", [Dir]).
remove_all_files(Dir, Files) ->
	lists:foreach(fun(File) ->
						  FilePath = filename:join([Dir, File]),
						  case filelib:is_dir(FilePath) of
							  true ->
								  {ok, DirFiles} = file:list_dir(FilePath),
								  remove_all_files(FilePath, DirFiles),
								  file:del_dir(FilePath);
							  _ ->
								  file:delete(FilePath)
						  end
				  end, Files).
```


